
allprojects {
	    group = "com.hoddmimes.jsontransform"
	    version = 1.0
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8

    sourceSets {
       main {
          java {
            srcDir 'java/main'
          }
       }
       test {
         java {
            srcDir 'java/test'
        }
      }
   }

   repositories {
      mavenCentral()
   }

   dependencies {
    compile group: 'com.github.erosb', name: 'everit-json-schema-jdk6', version: '1.9.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '3.8.2'
    compile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'xalan', name: 'xalan', version: '2.7.2'
   }
}


project(':jtransform') {

    task buildGenerateJar (type: Jar) {
        baseName = 'pojojson-generate';
        version = '1.0'
        destinationDir = file("../")

        from {
            configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        }
        from('build/classes/java/main') {
            include '**/*.class'
        }

        from('xsl') {
            include '*.xsl'
        }

        doLast {
            println('Generate JAR successfully built!')
        }
    }

    
    build.finalizedBy(buildGenerateJar)
    
    dependencies {
    }
}




project(':sample') {
    
    task(generateTestClasses, type: JavaExec) {
        main = 'JsonTransform'
        classpath = files('../pojojson-generate-1.0.jar')
        args "-xml","./xml/TestMessagesFileSet.xml"
    }

    
    task buildJars() {
        dependsOn 'buildPOJOJSONJar'
        dependsOn 'buildTestJar'
        
        doLast {
            println('Successfully build the pojojson and pojojson-test JARS !') 
        }
    }

    task buildTestJar (type: Jar) {
        baseName = 'pojojson-tests';
        version = '1.0'
        destinationDir = file("../")

        from {
            configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        }
        from('out/production/classes') {
            include '**/*.class'
        }

        from('out/test/classes') {
            include '**/*.class'
        }
    }
    
    task buildPOJOJSONJar (type: Jar) {
        baseName = 'pojojson';
        version = '1.0'
        destinationDir = file("../")
        
        from('out/production/classes') {
            include '**/jsontransform/*.class'
        }
    }

    
    

    compileJava.dependsOn('generateTestClasses')
    build.finalizedBy(buildJars) 
    
    dependencies {
        compile project(':jtransform')
    }
}










